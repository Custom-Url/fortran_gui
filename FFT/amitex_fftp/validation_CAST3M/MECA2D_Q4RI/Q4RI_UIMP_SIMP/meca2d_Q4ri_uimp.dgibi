*
* POUR COMPARAISON AVEC DEV AMITEX UIMPOSE AU CONTOUR
*

OPTI DIME 2 ELEM QUA4 MODE PLAN DEFO;
OPTI EPSILON LINEAIRE;

* DONNEES ENTREE
* 30, 20 OU 21
NEL=30;
RAY0 = 0.3;

* REPA0 = 1 FORCES DUPLIQUEES SUR 4 SOMMETS 
*         0 SUR UN SOMMET
REPA0 = 0;

EXX0 = 0.01;
EYY0 = 0.;
EXY0 = 0.;

SXX0 = 1e8;
SYY0 = 0.;
SXY0 = 0.;

EMAT   = 1.e5;
NUMAT  = 0.3;
EINCL  = 1.e0;
NUINCL = 0.3;

*CLP0 = 0 : Def homogene
*CLP0 = 1 : Periodique
*CLP0 = 2 : Contrainte Homogene

CLP0 = 0;
*CLP0 = 1;
*CLP0 = 2;

* MAILLAGE REGULIER
*------------------

A = 0. 0.;
B = 1. 0.;
C = 1. 1.;
D = 0. 1.;

AB = DROI NEL A B;
BC = DROI NEL B C;
CD = DROI NEL C D;
DA = DROI NEL D A;

SURF0 = DALL AB BC CD DA;

* MODELE ET MATERIAU
*-------------------
MOD00 = MODE SURF0 MECANIQUE ELASTIQUE ISOTROPE Q4RI;

X0 = COOR 1 SURF0;
Y0 = COOR 2 SURF0;
X0c = CHAN 'CHAM' X0 MOD00 'STRESSES';
Y0c = CHAN 'CHAM' Y0 MOD00 'STRESSES';
D0c = (((X0c - 0.36)**2) +((Y0c - 0.36)**2))**0.5;

INCL0 = D0c ELEM INFERIEUR RAY0; 
MATR0 = D0c ELEM EGSUPE RAY0; 

*trac ((MATR0 COUL ROUGE) ET (INCL0 COUL VERTE));

MODM0 = MODE MATR0 MECANIQUE ELASTIQUE ISOTROPE Q4RI;
MODI0 = MODE INCL0 MECANIQUE ELASTIQUE ISOTROPE Q4RI;
MATM0 = MATE MODM0 YOUN EMAT NU NUMAT;
MATI0 = MATE MODI0 YOUN EINCL NU NUINCL;


* ASSEMBLAGE

MOD0 = MODM0 ET MODI0;
MAT0 = MATM0 ET MATI0;
RIGM0 = RIGI MOD0 MAT0;

* TRACE CHAMP DE PROPRIETES

YOUN0 = EXCO MAT0 YOUN;
trac YOUN0 MOD0;


* CL HOMOGENES OU PERIODIQUES 2D
*--------------------------------
TAB0 = TABLE;
TAB0 . 1 = EXX0;
TAB0 . 2 = EYY0;
TAB0 . 3 = EXY0;

TAB1 = TABLE;
TAB1 . 1 = SXX0;
TAB1 . 2 = SYY0;
TAB1 . 3 = SXY0;

RIG0 F0 = @CLDH2D SURF0 TAB0;
SI (CLP0 EGA 1);
RIG0 F0 = @CLPD2D SURF0 TAB0;
FINSI;
SI (CLP0 EGA 2);
RIG0 F0 = @CLCH2D SURF0 TAB1;
FINSI;

* SIMULATION
*---------------------
DEP0 = RESOU (RIGM0 ET RIG0) F0;

EPS1 = EPSI DEP0 MOD0 MAT0;
SIG1 = SIGM DEP0 MOD0 MAT0;
trac SIG1 MOD0;


* SORTIE DU CHAMP DE CONTRAINTE
*-------------------------------
OPTI SORT 'SXX_UIMP_30.vtk';
SI (CLP0 EGA 1);
OPTI SORT 'SXX_CLP_30.vtk';
FINSI;
SI (CLP0 EGA 2);
OPTI SORT 'SXX_SIMP_30.vtk';
FINSI;

SIGXX = EXCO SIG1 SMXX;
SORT VTK SIGXX  SURF0;

* CHAMP DE DEPLACEMENT AU POINT DE GAUSS
*---------------------------------------
DEP1 = CHAN CHAM DEP0 MOD0 'RIGIDITE';

DEP1X = EXCO DEP1 UX;
UXmax = MAXI DEP1X;
UXmin = MINI DEP1X;
UXmoy = ((UXmax + UXmin) / 2.);
UXmax = UXmax - UXmoy;
UXmin = UXmin - UXmoy;
list UXmax;
list UXmin;

DEP1Y = EXCO DEP1 UY;
UYmax = MAXI DEP1Y;
UYmin = MINI DEP1Y;
UYmoy = ((UYmax + UYmin) / 2.);
UYmax = UYmax - UYmoy;
UYmin = UYmin - UYmoy;
list UYmax;
list UYmin;

trac (DEP1X - UXmoy) MOD0;
trac (DEP1Y - UYmoy) MOD0;

OPTI SORT 'UX_UIMP_30.vtk';
SI (CLP0 EGA 1);
OPTI SORT 'UX_CLP_30.vtk';
FINSI;
SI (CLP0 EGA 2);
OPTI SORT 'UX_SIMP_30.vtk';
FINSI;
SORT VTK (DEP1X - UXmoy)  SURF0;

FIN;










