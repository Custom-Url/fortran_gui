*
* POUR COMPARAISON AVEC DEV AMITEX FVOL NODALE IMPOSE
*
* LE POSITIONNEMENT DES FORCES CORRESPOND AU CAS AMITEX AVEC 
*      RESOLUTION 21
*

OPTI DIME 2 ELEM QUA4 MODE PLAN DEFO;
OPTI EPSILON LINEAIRE;

* DONNEES ENTREE
* 20 OU 21
NEL=21;
RAY0 = 0.3;

* FVOL0 = FORCE VOLUMIQUE NODALE APPLIQUE DANS AMITEX
FVOL0 = 10.e10;

* REPA0 = 1 FORCES DUPLIQUEES SUR 4 SOMMETS 
*         0 SUR UN SOMMET
REPA0 = 0;

EXX0 = 0.;
EYY0 = 0.;
EXY0 = 0.;

EMAT   = 1000.e9;
NUMAT  = 0.3;
EINCL  = 1000.e9;
NUINCL = 0.3;



* MAILLAGE REGULIER
*------------------

A = 0. 0.;
B = 1. 0.;
C = 1. 1.;
D = 0. 1.;

AB = DROI NEL A B;
BC = DROI NEL B C;
CD = DROI NEL C D;
DA = DROI NEL D A;

SURF0 = DALL AB BC CD DA;

* POINTS POUR LES FORCES IMPOSEES
*--------------------------------
DX0 = (10-1)*(1./NEL);
SI (NEL EGA 21);
DY0 = (18-1)*(1./NEL);
FINSI;
SI (NEL EGA 20);
DY0 = (17-1)*(1./NEL);
FINSI;


dx = 1./NEL;
A0 = SURF0 POIN PROC (DX0 0.);
A1 = SURF0 POIN PROC ((DX0+dx) 0.);
A2 = SURF0 POIN PROC ((DX0+dx) (0.+dx));
A3 = SURF0 POIN PROC (DX0 (0.+dx));
C0 = SURF0 POIN PROC (DX0 DY0);
C1 = SURF0 POIN PROC ((DX0+dx) DY0);
C2 = SURF0 POIN PROC ((DX0+dx) (DY0+dx));
C3 = SURF0 POIN PROC (DX0 (DY0+dx));

* MODELE ET MATERIAU
*-------------------
MOD00 = MODE SURF0 MECANIQUE ELASTIQUE ISOTROPE Q4RI;

X0 = COOR 1 SURF0;
Y0 = COOR 2 SURF0;
D0 = (((X0 - 0.5)**2) +((Y0 - 0.5)**2))**0.5;
D0c = CHAN 'CHAM' D0 MOD00 'STRESSES';

INCL0 = D0c ELEM INFERIEUR RAY0; 
MATR0 = D0c ELEM EGSUPE RAY0; 

*trac ((MATR0 COUL ROUGE) ET (INCL0 COUL VERTE));

MODM0 = MODE MATR0 MECANIQUE ELASTIQUE ISOTROPE Q4RI;
MODI0 = MODE INCL0 MECANIQUE ELASTIQUE ISOTROPE Q4RI;
MATM0 = MATE MODM0 YOUN EMAT NU NUMAT;
MATI0 = MATE MODI0 YOUN EINCL NU NUINCL;


* ASSEMBLAGE

MOD0 = MODM0 ET MODI0;
MAT0 = MATM0 ET MATI0;
RIGM0 = RIGI MOD0 MAT0;


* CL PERIODIQUE 2D
*--------------------
TAB0 = TABLE;
TAB0 . 1 = EXX0;
TAB0 . 2 = EYY0;
TAB0 . 3 = EXY0;

RIG0 F0 = @CLPD2D SURF0 TAB0;

*    FORCES NODALES IMPOSEES
*----------------------------
SA0 = A0;
SC0 = C0;
SI (REPA0 EGA 1);
SA0 = A0 ET A1 ET A2 ET A3;
SC0 = C0 ET C1 ET C2 ET C3;
FINSI;

* Fvol0*dx^3/dx -> ok pour amitex/matlab
FE0 = FVOL0*dx;
FE0 = FVOL0*dx*dx;
SI (REPA0 EGA 1);
FE0 = 4*FVOL0*dx ;
* ci-dessous -> ok pour amitex/matlab
FE0 = FVOL0*dx*dx;
FINSI;
FA0 = FORCE (0. FE0) SA0;
FC0 = FORCE (0. (0. - FE0)) SC0;

* SIMULATION
*---------------------
DEP0 = RESOU (RIGM0 ET RIG0) (F0 ET FA0 ET FC0);
*DEP0 = RESOU (RIGM0 ET RIG0) (F0 ET FA0);

EPS1 = EPSI DEP0 MOD0 MAT0;
SIG1 = SIGM DEP0 MOD0 MAT0;
trac SIG1 MOD0;
*

* SORTIE DU CHAMP DE CONTRAINTE
*-------------------------------
OPTI SORT 'SXX_21.vtk';
SIGXX = EXCO SIG1 SMXX;
SORT VTK SIGXX  SURF0;

* CHAMP DE DEPLACEMENT AU POINT DE GAUSS
*---------------------------------------
DEP1 = CHAN CHAM DEP0 MOD0 'RIGIDITE';

DEP1X = EXCO DEP1 UX;
UXmax = MAXI DEP1X;
UXmin = MINI DEP1X;
UXmoy = ((UXmax + UXmin) / 2.);
UXmax = UXmax - UXmoy;
UXmin = UXmin - UXmoy;
list UXmax;
list UXmin;

DEP1Y = EXCO DEP1 UY;
UYmax = MAXI DEP1Y;
UYmin = MINI DEP1Y;
UYmoy = ((UYmax + UYmin) / 2.);
UYmax = UYmax - UYmoy;
UYmin = UYmin - UYmoy;
list UYmax;
list UYmin;

trac (DEP1X - UXmoy) MOD0;
trac (DEP1Y - UYmoy) MOD0;

OPTI SORT 'UX_21.vtk';
SORT VTK (DEP1X - UXmoy)  SURF0;

FIN;










