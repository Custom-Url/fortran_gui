OPTI DIME 2 ELEM QUA4 MODE PLAN DEFO;
OPTI EPSILON LINEAIRE;

* DONNEES ENTREE

NEL=20;
RAY0 = 0.3;

EXX0 = 1.;
EYY0 = 0.;
EXY0 = 0.;

EMAT   = 1000.e9;
NUMAT  = 0.3;
EINCL  = 1e9;
NUINCL = 0.3;


* MAILLAGE REGULIER
*------------------

A = 0. 0.;
B = 1. 0.;
C = 1. 1.;
D = 0. 1.;

AB = DROI NEL A B;
BC = DROI NEL B C;
CD = DROI NEL C D;
DA = DROI NEL D A;

SURF0 = DALL AB BC CD DA;


* MODELE ET MATERIAU
*-------------------
MOD00 = MODE SURF0 MECANIQUE ELASTIQUE ISOTROPE Q4RI;

X0 = COOR 1 SURF0;
Y0 = COOR 2 SURF0;
D0 = (((X0 - 0.5)**2) +((Y0 - 0.5)**2))**0.5;
D0c = CHAN 'CHAM' D0 MOD00 'STRESSES';

INCL0 = D0c ELEM INFERIEUR RAY0; 
MATR0 = D0c ELEM EGSUPE RAY0; 

*trac ((MATR0 COUL ROUGE) ET (INCL0 COUL VERTE));

MODM0 = MODE MATR0 MECANIQUE ELASTIQUE ISOTROPE Q4RI;
MODI0 = MODE INCL0 MECANIQUE ELASTIQUE ISOTROPE Q4RI;
MATM0 = MATE MODM0 YOUN EMAT NU NUMAT;
MATI0 = MATE MODI0 YOUN EINCL NU NUINCL;


* ASSEMBLAGE

MOD0 = MODM0 ET MODI0;
MAT0 = MATM0 ET MATI0;
RIGM0 = RIGI MOD0 MAT0;


* CL PERIODIQUE 2D
*--------------------
TAB0 = TABLE;
TAB0 . 1 = EXX0;
TAB0 . 2 = EYY0;
TAB0 . 3 = EXY0;

RIG0 F0 = @CLPD2D SURF0 TAB0;

* SIMULATION
*---------------------
DEP0 = RESOU (RIGM0 ET RIG0) F0;

EPS1 = EPSI DEP0 MOD0 MAT0;
SIG1 = SIGM DEP0 MOD0 MAT0;
trac SIG1 MOD0;
*

* SORTIE DU CHAMP
*-----------------
OPTI SORT 'SXX_128.vtk';
SIGXX = EXCO SIG1 SMXX;
SORT VTK SIGXX  SURF0;


FIN;










